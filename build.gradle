/*
 * Created by brightSPARK Labs
 * www.brightsparklabs.com
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the brightSPARK Labs plugin for standardisation.
    id 'com.brightsparklabs.gradle.baseline' version "4.1.0"

    // Apply the java-library plugin to add support for Java Library.
    id 'java-library'

    // Apply the groovy plugin to also add support for Groovy (needed for Spock).
    id 'groovy'

    // Apply the nexus publish plugin for publishing to Maven Central repository.
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"

    // -----------------------------------------------------------------------------
    // PUBLISHING
    // -----------------------------------------------------------------------------

    id 'maven-publish'
    // Maven Central requires artifacts to be signed.
    id 'signing'
}

sourceCompatibility = 17
targetCompatibility = 17

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description  = 'Dropwizard bundle for trusting externally authenticated users'
project.ext.url      = 'https://github.com/brightsparklabs/dropwizard-external-auth-bundle'
project.ext.scm      = 'scm:git@github.com:brightsparklabs/dropwizard-external-auth-bundle.git'

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    // Use mavenCentral for resolving dependencies.
    mavenCentral()
}

dependencies {
    def versions = [
        dropwizard: '4.0.0',
        immutables: '2.9.3',
        jjwt      : '0.11.5',
    ]
    // These dependencies are found on compile classpath of this component and consumers.
    implementation(
            // General
            'com.google.guava:guava:31.1-jre',
            "io.jsonwebtoken:jjwt-api:${versions.jjwt}",

            // Dropwizard
            "io.dropwizard:dropwizard-core:${versions.dropwizard}",
            "io.dropwizard:dropwizard-auth:${versions.dropwizard}",
            )

    compileOnly(
            "org.immutables:value:${versions.immutables}",
            // We add this so that Immutables will annotate its generated classes with
            // @SuppressFBWarnings, and thus avoid Findbugs errors for those generated classes.
            'com.google.code.findbugs:annotations:3.0.1'
            )

    runtimeOnly(
            "io.jsonwebtoken:jjwt-impl:${versions.jjwt}",
            "io.jsonwebtoken:jjwt-jackson:${versions.jjwt}",
            )

    testImplementation(
            'org.apache.groovy:groovy-all:4.0.11',
            // Use the awesome Spock testing and specification framework even with Java
            'org.spockframework:spock-core:2.4-M1-groovy-4.0',
            // Allow spock to mock non-interfaces
            'net.bytebuddy:byte-buddy:1.14.4',
            'org.junit.jupiter:junit-jupiter-api:5.9.3',
            )

    annotationProcessor(
            "org.immutables:value:${versions.immutables}",
            )
}
// -----------------------------------------------------------------------------
// SETUP ARTIFACTS FOR PUBLISHING
// -----------------------------------------------------------------------------

// Maven Central requires javadoc and sources.
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java

            // Maven Central requires specific POM attributes.
            pom {
                name = project.name
                description = project.description
                url = project.url

                scm {
                    connection = project.scm
                    developerConnection = project.scm
                    url = project.scm
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'brightsparklabs'
                        name = 'brightSPARK Labs'
                        email = 'enquire@brightsparklabs.com'
                    }
                }
            }
        }
    }
}

// Maven Central requires signed artifacts.
signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava

    // Only require signing when publishing (otherwise normal builds would need GPG keys).
    required { gradle.taskGraph.hasTask("publishToSonatype") }
}

// -----------------------------------------------------------------------------
// PUBLISH ARTIFACTS (via nexus)
// -----------------------------------------------------------------------------

nexusPublishing {
    repositories {
        // Credentials default to:
        // - Property `sonatypeUsername` or ENV variable `ORG_GRADLE_PROJECT_sonatypeUsername`.
        // - Property `sonatypePassword` or ENV variable ORG_GRADLE_PROJECT_sonatypePassword`.
        sonatype()
    }
}

tasks.register('prePublishToMavenCentral') {
    group 'brightSPARK Labs - Maven Central Publishing'
    description 'Stages the release on Sonatype ready for publishing to Maven Central.'
    dependsOn 'publishToSonatype', 'closeSonatypeStagingRepository'
}
tasks.register('publishToMavenCentral') {
    group 'brightSPARK Labs - Maven Central Publishing'
    description 'Publishes the release to Maven Central.'
    dependsOn 'publishToSonatype', 'closeAndReleaseSonatypeStagingRepository'
}
